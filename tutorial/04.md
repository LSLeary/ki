[Previous tutorial](03.md)

---

# Tutorial 4

In this tutorial, we'll play around with nesting __scopes__ and __threads__.

```haskell
{-# LANGUAGE BlockArguments #-}
module Tutorial04 where
import qualified Ki.Lite as Ki
main = pure () :: IO ()
```

First, let's spawn two helper __threads__ in a __scope__, and have one of them
spawn their _own_ helper __thread__!

```haskell
example1 :: IO ()
example1 =
  Ki.scoped \scope -> do
    -- A fork within a fork!
    Ki.fork scope (Ki.fork scope (putStrLn "Hello from helper 1!"))
    Ki.fork scope (putStrLn "Hello from helper 2!")
    Ki.wait scope
```

It works! But wait, we did pass that __scope__ awful far down, didn't we? In
fact, if we factored our helper __threads__ into helper _functions_, the first
one would require its own __scope__ as input.

```haskell
example2 :: IO ()
example2 = do
  Ki.scoped \scope -> do
    Ki.fork scope (helper1 scope) -- Oops!
    Ki.fork scope helper2
    Ki.wait scope

  where
    -- Smelly type signature.
    helper1 :: Ki.Scope -> IO ()
    helper1 scope = do
      Ki.fork scope (putStrLn "Hello from helper 1!")

    helper2 :: IO ()
    helper2 = do
      putStrLn "Hello from helper 2!"
```

This is a structured concurrency no-no. It's occasionally necessary, but it's an
encapsulation violation that makes reasoning about your program more difficult.
A __thread__ that has access to its own __scope__ can spawn its own "siblings",
which each have the ability to bring the entire __scope__ down if they throw an
exception.

The simplest and most straightforward usage of a __scope__ is to spawn
__threads__ only at the outermost lexical scope, and never give those
__threads__ access to their own __scope__. That makes it easier to understand
the concurrent nature of your program at a glance.

Let's refactor the example and remove this encapsulation violation.

```haskell
example3 :: IO ()
example3 = do
  Ki.scoped \scope -> do
    -- It's easy to see where `scope` is used.
    Ki.fork scope helper1
    Ki.fork scope helper2
    Ki.wait scope

  where
    helper1 :: IO ()
    helper1 = do
      Ki.scoped \scope -> do
        Ki.fork scope (putStrLn "Hello from helper 1!")
        Ki.wait scope

    helper2 :: IO ()
    helper2 = do
      putStrLn "Hello from helper 2!"
```

Much better! Upon visual inspection, it's now clear exactly how deep the
top-level __scope__ actually extends:

```haskell ignore
Ki.scoped \scope -> do
  Ki.fork scope helper1
  Ki.fork scope helper2
  Ki.wait scope
```

Compare this to our first attempt, where `helper1` was provided its own
__scope__:

```haskell ignore
Ki.scoped \scope -> do
  Ki.fork scope (helper1 scope) -- Oops!
  Ki.fork scope helper2
  Ki.wait scope
```

In our second attempt, the helper functions `helper1` and `helper2` may spawn
__threads__ of their own, and in fact `helper1` does, but whether they do or not
is (helpfully) not very relevant to us trying to understand the overall behavior
of the code. We know that by the time each finishes, any __threads__ they may
have spawned have finished, too. That's structured concurrency in a nutshell.

---

[Previous tutorial](03.md)
